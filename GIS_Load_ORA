/**
* Name: Peso por carretera  
* Author:Oscar Rojo
* Description: cuarta parte del tutorial semana 1
* Tags: graph
*/

model Peso_importancia_por_carretera

global {
	file archivo_edificios <- file("../includes/building.shp");
	file archivo_carreteras <- file("../includes/road.shp");
	file archivo_limites <- file("../includes/bounds.shp");
	geometry shape <- envelope(archivo_limites);
	float step <- 10 #mn;
	date starting_date <- date("2019-09-01-00-00-00");
	int numerodepersonas <- 100;
	int mininiciotrabajo <- 6;
	int maxiniciotrabajo <- 8;
	int minfintrabajo <- 16;
	int maxfintrabajo <- 20;
	float minvelocidad <- 1.0 #km / #h;
	float maxvelocidad <- 5.0 #km / #h;
	graph the_graph;
	float trafico <- 0.02; 
	

	init {
		create building from: archivo_edificios with: [type::string(read("NATURE"))] {
			if type = "Industrial" {
				color <- #black;
			} else {
				color <- #orange;
			}
		}
		create road from: archivo_carreteras;
		map<road, float> mapaconpeso <- road as_map(each::(each.coeficiente * each.shape.perimeter));
		the_graph <- as_edge_graph(road) with_weights mapaconpeso;

		list<building> residential_buildings <- building where (each.type = "Residential");
		list<building> industrial_buildings <- building where (each.type = "Industrial");

		create personas number: numerodepersonas {
			velocidad <- rnd(minvelocidad, maxvelocidad);
			iniciotrabajo <- rnd(mininiciotrabajo, maxiniciotrabajo);
			fintrabajo <- rnd(minfintrabajo, maxfintrabajo);
			vivienda <- one_of(residential_buildings);
			lugardetrabajo <- one_of(industrial_buildings);
			objetivo <- "resting";
			location <- any_location_in(vivienda);
		}
	}
	reflex update_graph{ 
		map<road,float> mapaconpeso <- road as_map (each:: (each.coeficiente * each.shape.perimeter));
		the_graph <- the_graph with_weights mapaconpeso;
	}
	
	
}

species building {
	string type;
	rgb color <- #gray;

	aspect base {
		draw shape color: color;
	}
}

species road {
	float coeficiente <- rnd(1.0, 2.0) max: 2.0;
	int colorValue <- int(255 * (coeficiente - 1)) update: int(255 * (coeficiente - 1));
	rgb color <- rgb(min([255, colorValue]), max([0, 255 - colorValue]), 0) update: rgb(min([255, colorValue]), max([0, 255 - colorValue]), 0);

	aspect base {
		draw shape color: color;
	}
}

species personas skills: [moving] {
	rgb color <- #yellow;
	building vivienda <- nil;
	building lugardetrabajo <- nil;
	int iniciotrabajo;
	int fintrabajo;
	string objetivo;
	point the_target <- nil;
	float velocidad;

	reflex horadetrabajo when: current_date.hour = iniciotrabajo and objetivo = "resting" {
		objetivo <- "working";
		the_target <- any_location_in(lugardetrabajo);
	}

	reflex horadecasa when: current_date.hour = fintrabajo and objetivo = "working" {
		objetivo <- "resting";
		the_target <- any_location_in(vivienda);
	}

	reflex mover when: the_target != nil {
		do goto target: the_target on: the_graph;
		if the_target = location {
			the_target <- nil;
		}
	}
	
		reflex move when: the_target != nil {
		path path_followed <- goto(target: the_target, on:the_graph, return_path: true);
		list<geometry> segments <- path_followed.segments;
		loop line over: segments {
			float dist <- line.perimeter;
			ask road(path_followed agent_from_geometry line) { 
				coeficiente <- coeficiente + (trafico * dist / shape.perimeter);
			}
		}
		if the_target = location {
			the_target <- nil ;
		}
	}

	aspect base {
		draw circle(8) color: color border: #black;
	}
}

experiment road_traffic type: gui {
	parameter "Shapefile para los edificios:" var: archivo_edificios category: "GIS";
	parameter "Shapefile para las carreteras:" var: archivo_carreteras category: "GIS";
	parameter "Shapefile para los límites:" var: archivo_limites category: "GIS";
	parameter "Número de personas" var: numerodepersonas category: "People";
	parameter "Hora mínima de inicio de trabajo" var: mininiciotrabajo category: "People" min: 2 max: 8;
	parameter "Hora máxima de inicio de trabajo" var: maxiniciotrabajo category: "People" min: 8 max: 12;
	parameter "Hora mínima de fin de trabajo" var: minfintrabajo category: "People" min: 12 max: 16;
	parameter "Hora máxima de fin de trabajo" var: maxfintrabajo category: "People" min: 16 max: 23;
	parameter "Velocidad mínima" var: minvelocidad category: "People" min: 0.1 #km/#h;
	parameter "Velocidad máxima" var: maxvelocidad category: "People" max: 10 #km/#h;
	parameter "Value of destruction when a people agent takes a road" var: trafico category: "Road" ;


	

	output {
		display city_display type: 3d {
			species building aspect: base;
			species road aspect: base;
			species personas aspect: base;
		}
	}
}
